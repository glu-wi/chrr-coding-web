---
title: SAS Macor (1)
author: ''
date: '2022-07-07'
slug: sas-macor-1
categories:
  - SAS
tags:
  - SAS Macro
output:
  blogdown::html_page:
    toc: true
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(echo = TRUE)

```

## Why Use Macros?

**Don't Repeat Yourself (DRY)**

-   Macros automatically generate SAS code

-   Make more dynamic, complex, and reusable SAS programs

-   Reduce the effort required to read, write, and modify SAS Code

---

## Two building blocks

-   <span style="color: blue;">__`macro variables`__</span>

  -   `&name` 
    -   refers to a macro variable
    -   like a standard data variable except that it does not belong to a data set and has only a single value which is always character
    
-   <span style="color: blue;">__`macros`__</span>

  -   `%name` 
    -   refers to a macro
    -   a piece of a program that can contain complex logic including DATA and PROC steps, and macro statements (e.g., `%IF-%THEN/%ELSE` and `%DO-%END`)
    -   Macros often —but not always—contain macro variables

---

## Built-In Macro Variables

|  | description | 
|------:|:-----|
| `&SYSDATE9` |  :stores the date the SAS session started (in DATE9. format, e.g., 02JUL2022)  |
|  `&SYSTIME`  |  :stores the time the SAS session started  | 
|    `&SYSDAY`  |    :stores day of the week the session started | 
| ... | ... |


```{sas eval=FALSE, echo=T}
proc print data = sashelp.cars;
	where make = 'Audi' and type = 'Sports' ;
	TITLE "Sales as of &SYSDAY &SYSDATE";  #<<
run;
```

-   `%PUT _AUTOMATIC_;` 
    -   lists all automatic macro variables created by SAS
    -   It instructs the macro processor to write information to the SAS log

---

## Double vs. Single Quotes

-   If you place a macro variable inside quotation marks, SAS resolves only macro variables that are inside `double` quotation marks

```{sas eval=FALSE, echo=T}
proc print data = sashelp.cars;
	where make = 'Audi' and type = 'Sports' ;
	TITLE "Sales as of &SYSDAY &SYSDATE";  #<<
run;
```

-   A macro variable reference enclosed within single quotation marks is not resolved

```{sas eval=FALSE, echo=T}
proc print data = sashelp.cars;
	where make = 'Audi' and type = 'Sports' ;
	TITLE 'Sales as of &SYSDAY &SYSDATE';  #<<
run;
```


---

## Creating Macro Variables

-   %LET statement

```{sas eval=FALSE, echo=T}
%let year = 2022; #<<
```

-   PROC SQL

```{sas eval=FALSE, echo=T}
proc sql;
select count(*)
	into :total  #<<
	from sashelp.cars;
quit;
```

-   DATA step

```{sas eval=FALSE, echo=T}
data _null_;
CALL SYMPUTX ('today', PUT("&sysdate"d,worddate22.));  #<<
run;
%put &today;
```

---

## Assign Values to Macro Variables 

-   use a `%LET` Statement

```{r eval=FALSE, echo=T}
%let n = 5;

data want;
	do i = 1 to &n;
	x = int(100*ranuni(0) + 1);
	output;
	end;
run;

title "Data Set with &n Random Numbers";
proc print data=want noobs;
run;
```

Notice that the `%LET` statement comes before the DATA step. 
When this program runs, each occurrence of &n is replaced with a 5.

---

## Transfer a Value between DATA Steps

-   CALL SYMPUT
-   CALL SYMPUTX: Assigns a value to a macro variable, and removes both leading and trailing blanks.

Suppose we want to compute the mean of MSRP from CARS data set and compare each individual value of MSRP against the mean
```{r eval=FALSE, echo=T}
data have; set sashelp.cars (keep=make type origin msrp);
  where type = 'Sports' and origin = "Europe";
run;

proc means data=have noprint;
  var msrp;
  output out=means mean= m_msrp;
run;

data _null_; set means;
  call symputx('AveMSRP', m_msrp); #<<
run;

data want; set have;
  Per_mspr = msrp / &AveMSRP; #<<
  format Per_mspr percent8.;
run;
```

---

## Use PROC SQL

```{r eval=FALSE, echo=T}
data have; set sashelp.cars (keep=make type origin msrp);
  where type = 'Sports' and origin = "Europe";
run;

proc sql;
select avg(msrp)
	into :AveMSRP2  #<<
from have;
quit;

data want2; set have;
  Per_mspr = msrp / &AveMSRP2; #<<
  format Per_mspr percent8.;
run;
```

---

## Macros
-   Defining a Macro
```{r eval=FALSE, echo=T}
%MACRO macro-name;
  macro-text
%MEND <macro-name>;
```

-   Calling a Macro
```{r eval=FALSE, echo=T}
%macro-name
```

-   Example

```{r eval=FALSE, echo=TRUE}
%macro today;
  %put Today is &sysday &sysdate9;
%mend;

%today
```

---

## Macros - example

```{r eval=FALSE, echo=TRUE}
%macro gen_rand(n, Start, End);
  data rand_data;
  do i = 1 to &n;
    x = int((&End - &Start + 1)*ranuni(0) + &Start);
    output;
  end;
  run;
  proc print data=rand_data noobs;
    title "Randomly Generated Data Set with &n Obs";
    title2 "Values are from &Start to &End";
  run;
%mend gen_rand;

%gen_rand(5,1,10)
```
